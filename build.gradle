plugins {
    id 'application'
    id 'idea'
    id 'java'
    id 'com.google.protobuf' version '0.8.10'
    id 'org.jetbrains.kotlin.jvm' version '1.3.50'
    id "com.github.ben-manes.versions" version "0.27.0"
}

group 'org.athenian'
version '1.0-SNAPSHOT'

sourceCompatibility = 1.8

repositories {
    maven { // The google mirror is less flaky than mavenCentral()
        url "https://maven-central.storage-download.googleapis.com/repos/central/data/"
    }
    mavenLocal()
}

def kotlinVersion = '1.3.50'
def coroutinesVersion = '1.3.1'
def grpcVersion = '1.25.0'
def protocVersion = '3.10.0'
def annotationVersion = "1.3.2"
def junitVersion = '4.12'
def slf4Version = '1.7.28'
def loggingVersion = '1.7.6'

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"

    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:${coroutinesVersion}"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-jdk8:${coroutinesVersion}"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-slf4j:${coroutinesVersion}"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-debug:${coroutinesVersion}"

    implementation "io.grpc:grpc-netty-shaded:${grpcVersion}"
    implementation "io.grpc:grpc-protobuf:${grpcVersion}"
    implementation "io.grpc:grpc-stub:${grpcVersion}"
    implementation "io.grpc:grpc-services:${grpcVersion}"

    implementation "org.slf4j:slf4j-simple:${slf4Version}"
    implementation "io.github.microutils:kotlin-logging:${loggingVersion}"

    compileOnly "javax.annotation:javax.annotation-api:${annotationVersion}"

    testImplementation "io.grpc:grpc-testing:${grpcVersion}"

    testImplementation "junit:junit:$junitVersion"
    testCompile group: 'junit', name: 'junit', version: junitVersion
}

sourceSets {
    main {
        java {
            srcDirs 'src/main/java'
            srcDirs 'src/main/kotlin'
            srcDirs 'build/generated/source/proto/main/grpc'
            srcDirs 'build/generated/source/proto/main/java'
        }
    }
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:${protocVersion}"
    }
    plugins {
        grpc { artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}" }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
}

compileKotlin.dependsOn ':generateProto'

startScripts.enabled = false

task ChunkerServer(type: CreateStartScripts) {
    mainClassName = 'org.athenian.chunker.ChunkerServer'
    applicationName = 'chunker-server'
    outputDir = new File(project.buildDir, 'tmp')
    classpath = startScripts.classpath
}

task ChunkerClient(type: CreateStartScripts) {
    mainClassName = 'org.athenian.chunker.ChunkerClient'
    applicationName = 'chunker-client'
    outputDir = new File(project.buildDir, 'tmp')
    classpath = startScripts.classpath
}

applicationDistribution.into('bin') {
    from(ChunkerServer)
    from(ChunkerClient)
    fileMode = 0755
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
        freeCompilerArgs = ['-XXLanguage:+InlineClasses']
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
